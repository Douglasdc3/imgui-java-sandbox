import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
}

version '1.0'
sourceCompatibility = 1.11

project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.9.25"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = 'natives-linux'
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = 'natives-windows'
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = 'natives-macos'
        // jvmargs = "-XstartOnFirstThread";
        break
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
}

task start(type: JavaExec, dependsOn: build) {
    main = 'Main'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Djava.library.path=${System.getProperty('libPath', '../bin')}"]

    if (OperatingSystem.current().isMacOsX()) {
        jvmArgs += ['-XstartOnFirstThread', '-Djava.awt.headless=true']
    }
}
